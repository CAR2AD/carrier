using <stdio.h>::{printf};
using <string.h>::{memcpy, memset};
using <assert.h>::{assert};
using err;
using channel;
using router;
using bootstrap;
using net;
using io;
using time;
using endpoint;
using router;
using hex;
using log;
using pq;
using byteorder;
using identity;
using protonerf;
using buffer;
using stream;
using slice;
using net::address;
using hpack;
using slice::{Slice};
using peering;
using proto;
using noise;
using responder;
using mem;

static stream::Config PublishStream = stream::Config {
    path:   "/carrier.broker.v1/broker/publish",
    stream: stream_to_publish,
    close:  close_publish,
};


export fn publish(endpoint::Endpoint mut*self, err::Err+et mut*e)
    where err::checked(*e)
{
    self->broker_stream_registry[0] = &PeerConnect;

    stream::Stream mut* streami = self->broker()->open(e, &PublishStream);
    if err::check(e) {
        return;
    }
    static_attest(safe(streami));
    streami->user2 = self;
}

fn close_publish(stream::Stream mut *self, err::Err+et mut*e) {
    log::error("publish closed\n");
    err::assert_safe(self->chan);
    err::assert_safe(self->chan->endpoint);
    self->chan->endpoint->close();
}

fn stream_to_publish(stream::Stream mut *self, err::Err+et mut*e, Slice bs) -> bool
    where err::checked(*e)
    where slice::slice::integrity(&bs)
{
    static_attest(safe(self->chan));
    if self->state == 0 {
        log::info("publish response headers:");
        new it = hpack::decoder::decode(bs);
        while it.next(e) {
            log::info("  %.*s: %.*s", it.key.size, it.key.mem, it.val.size, it.val.mem);
        }
        if err::check(e) {
            return false;
        }


        let mut frame = self->chan->q.alloc(e, channel::FrameType::Stream, 100);
        if err::check(e) {
            return false;
        }

        self->state = 1;

        frame.push32(byteorder::to_be32(1)); // stream
        frame.push64(byteorder::to_be64(2)); // order
        frame.push16(0); // size placeholder

        static_attest(safe(self->chan->endpoint));


        identity::Address mut network = {0};
        self->chan->endpoint->vault.get_network(&network);
        protonerf::encode_bytes(frame, e, proto::PublishRequest::Network, network.k, 32);
        if err::check(e) {
            return false;
        }

        protonerf::encode_varint(frame, e, proto::PublishRequest::ReceiveAlias, 1);
        if err::check(e) {
            return false;
        }

    } else if self->state == 1 {
        new mut dec = protonerf::decode(bs);
        for (protonerf::Field mut field; dec.next(e,&field);) {
            if err::check(e) {
                return true;
            }
            switch field.index {
                proto::PublishChange::Alias => {
                    static_attest(safe(field.a));
                    static_attest(len(field.a) >= field.value.v_len);
                    new mut dec2 = protonerf::decode(slice::slice::Slice{mem: field.a, size: field.value.v_len});
                    for (protonerf::Field mut field; dec2.next(e,&field);) {
                        switch field.index {
                            proto::Alias::Alias => {
                                if field.a != 0 {
                                    log::info("alias %.*s", field.value.v_len, field.a);
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    return true;
}

static stream::Config PeerConnect = stream::Config {
    path:       "/carrier.broker.v1/peer/connect",
    open:       on_remote_open,
    stream:     stream_connect,
};


pub fn on_remote_open(stream::Stream mut*self, err::Err+et mut*e, slice::Slice headers)
    where slice::slice::integrity(&headers)
    where err::checked(*e)
{
    static_attest(safe(self->chan));
    let mut frame = self->stream(e, 14);
    if err::check(e) { return; }

    hpack::encoder::encode(frame, e, (u8*)":status", 7, (u8*)"200", 3);
    if err::check(e) { return ; }
}

fn stream_connect(stream::Stream mut *self, err::Err+et mut*e, slice::Slice msg) -> bool
    where slice::slice::integrity(&msg)
    where err::checked(*e)
{
    static_attest(safe(self->chan));

    if self->state == 0 {
        self->state = 1;
        identity::Identity mut ii = {0};
        bool mut has_identity = false;
        slice::slice::Slice mut handshake = {0};
        u64 mut timestamp = 0;
        u64 mut route     = 0;
        peering::Peering mut peerings = {0};
        memcpy(&peerings, &self->chan->peering, sizeof(peering::Peering));


        // -----------
        // 1. decode the protobuf message sent by the broker

        new mut dec = protonerf::decode(msg);
        for (protonerf::Field mut field;dec.next(e, &field);) {
            if err::check(e) {
                return true;
            }
            switch field.index {
                proto::PeerConnectRequest::Identity => {
                    if field.a == 0 || field.value.v_len != 32 {
                        err::fail(e, channel::InvalidFrame, "unexpected proto wire");
                        return false;
                    }
                    memcpy(ii.k, field.a, 32);

                    char mut m[60];
                    usize l = identity::identity_to_str(e, m, 60, &ii);
                    if err::check(e) {
                        return true;
                    }
                    log::info("connect request from identity %.*s", (int)l, m);
                    has_identity = true;
                }
                proto::PeerConnectRequest::Timestamp => {
                    timestamp = field.value.v_u64;
                }
                proto::PeerConnectRequest::Handshake => {
                    handshake.mem  = field.a;
                    handshake.size = field.value.v_len;
                }
                proto::PeerConnectRequest::Route => {
                    route = byteorder::to_be64(field.value.v_u64);
                }
                proto::PeerConnectRequest::Paths => {
                    peerings.from_proto(e, field);
                    if err::check(e) {
                        return true;
                    }
                }
                default => {
                }
            }
        }

        if !has_identity || handshake.mem == 0 || timestamp == 0 || route == 0 {
            err::fail(e, channel::InvalidFrame, "missing protobuf field");
            return true;
        }
        static_attest(len(handshake.mem) >= handshake.size);


        // -----------
        // 2. check the handshake packet
        // unfortunate misdesign of tunneling an entire init packet
        // which actually contains no interesting information

        if handshake.size < 1 + 3 + 8 + 8 + 32 + 16 + 3 + 32 + 8 {
            err::fail(e, channel::InvalidFrame, "too small");
            return true;
        }
        handshake.mem  = handshake.mem  + (1 + 3 + 8 + 8);
        handshake.size = handshake.size - (1 + 3 + 8 + 8);

        noise::Responder mut rsp = {0};
        identity::secret_generate(&rsp.ephemeral, e);
        rsp.receive_insecure(
                (u8*)endpoint::PROLOGUE,
                static(len(endpoint::PROLOGUE) - 1),
                handshake.mem,
                handshake.size - 64
        );
        if err::check(e) {
            return true;
        }


        // verify signature over symmetric hash
        u8 mut signme[static(len(endpoint::SIGN_PURPOSE) - 1 + len(rsp.symm.h))] = {0};
        mem::copy(endpoint::SIGN_PURPOSE, signme, buffer::strlen(endpoint::SIGN_PURPOSE));
        u8 mut * m = signme + buffer::strlen(endpoint::SIGN_PURPOSE);
        mem::copy(rsp.symm.h, m, static(len(rsp.symm.h)));

        identity::Signature mut signature = {0};
        mem::copy(handshake.mem + (handshake.size - 64), signature.k, 64);

        if !identity::verify(&ii, &signature, signme, static(len(signme))) {
            err::fail(e, channel::InvalidFrame, "signature verification failed. THIS IS A HACKING ATTEMPT");
            return true;
        }


        // match the handshake identity with what the broker thinks is the identity
        let mut id = handshake;
        id.mem  = id.mem  + 32;
        id.size = 32;
        if !id.eq_bytes(ii.k, 32) {
            err::fail(e, channel::InvalidFrame, "identity mismatch");
            return true;
        }

        self->broker_route = route;

        // get revision, if any
        u32 mut rev = 0;
        if handshake.size >= 32 + 32 + 8 + 4 {
            memcpy(&rev, handshake.mem + 32 + 32 + 8, 4);
            rev = byteorder::from_be32(rev);
        }
        log::info("initiator revision %u", rev);



        //FIXME compare route
        //FIXME check signature

        static_attest(safe(self->chan->endpoint));
        let authorized = self->chan->endpoint->vault.authorize_connect(&ii);

        if !authorized {
            log::error("UNAUTHORIZED");

            let mut frame = self->chan->q.alloc(e, channel::FrameType::Stream, 34);
            if err::check(e) {
                return true;
            }
            frame.push32(byteorder::to_be32(self->id));
            frame.push64(byteorder::to_be64((self->order_outgoing)++));
            frame.push16(0); // size placeholder

            protonerf::encode_varint(frame, e, proto::PeerConnectResponse::Ok, 0);
            if err::check(e) {
                return true;
            }
            protonerf::encode_bytes_start(frame, e, proto::PeerConnectResponse::Handshake , 0);
            if err::check(e) {
                return true;
            }
            protonerf::encode_bytes(frame, e, proto::PeerConnectResponse::Error, (u8*)"not authorized", 14);
            if err::check(e) {
                return true;
            }

            self->close();
            return true;
        }


        // -----------
        // 3. check if we can accept this request
        // the packet is ok from here and will be responded to

        endpoint::Endpoint mut * ep = self->chan->endpoint;
        err::assert_safe(ep);
        let nuchan = ep->statem.connected.next_channel();

        let mut frame = self->chan->q.alloc(e, channel::FrameType::Stream, 290);
        bool mut frameok = true;
        if err::check(e) {
            e->elog();
            frameok = false;
            e->make();
        }

        // oom
        if nuchan == 0 || !frameok {
            if nuchan == 0 {
                log::error("cannot accept: max channels");
            } else {
                log::error("cannot accept: max framepool");
            }

            if frameok  {
                self->chan->q.cancel();
                frameok = false;
            }

            let mut frame = self->chan->q.alloc(e, channel::FrameType::Stream, 30);
            if err::check(e) {
                return false;
            }
            frame.push32(byteorder::to_be32(self->id));
            frame.push64(byteorder::to_be64((self->order_outgoing)++));
            frame.push16(0); // size placeholder

            protonerf::encode_varint(frame, e, proto::PeerConnectResponse::Ok, 0);
            if err::check(e) {
                return true;
            }
            protonerf::encode_bytes_start(frame, e, proto::PeerConnectResponse::Handshake , 0);
            if err::check(e) {
                return true;
            }
            protonerf::encode_bytes(frame, e, proto::PeerConnectResponse::Error, (u8*)"out of mem", 10);
            if err::check(e) {
                return true;
            }

            self->close();
            return true;
        }
        static_attest(safe(nuchan));

        // -----------
        // 4. send connect response header

        frame.push32(byteorder::to_be32(self->id));
        frame.push64(byteorder::to_be64((self->order_outgoing)++));
        frame.push16(0); // size placeholder

        protonerf::encode_varint(frame, e, proto::PeerConnectResponse::Ok, 1);
        if err::check(e) {
            return true;
        }


        // handshake, full packet version 0x08
        protonerf::encode_bytes_start(frame, e, proto::PeerConnectResponse::Handshake, 252);
        if err::check(e) {
            return true;
        }

        static_attest(len(frame.mem) > *frame.at);
        static_attest(len(frame.mem) > *frame.at + 252);
        static_attest(safe(self->chan->store));

        responder::accept_insecure(
            &rsp,
            e,
            self->chan->store,
            route,
            timestamp,
            &frame
        );
        if err::check(e) {
            return true;
        }

        //TODO paths

        protonerf::encode_bytes(frame, e, proto::PeerConnectResponse::Error, (u8*)"",0);
        if err::check(e) {
            return true;
        }

        log::info("accepted %zu %lu\n", *frame.at, route);


        channel::from_transfer(nuchan, channel::transfer_from_symmetric(&rsp.symm, 0x08, rev, route, true));
        nuchan->q.allocator = self->chan->q.allocator;
        nuchan->store       = self->chan->store;
        nuchan->endpoint    = self->chan->endpoint;
        memcpy(&nuchan->peer,  &ii, sizeof(identity::Identity));
        memcpy(&nuchan->peering, &peerings, sizeof(peering::Peering));

    } else {
        log::error("broker error in peer connect");
        hex::dump_slice(msg);
        new it = hpack::decoder::decode(msg);
        while it.next(e) {
            log::error("  %.*s: %.*s", it.key.size, it.key.mem, it.val.size, it.val.mem);
        }

    }
    return true;

}

