using toml;
using err;
using vault;
using fs;
using buffer;
using identity;
using <stdio.h>::{fopen, fclose, fread, FILE, fgetc, EOF, fprintf, stderr, fputc};
using <string.h>::{memcpy, memset};
using <stdlib.h>::{malloc, free};
using <sys/stat.h>::{mkdir};
using log;
using time;
inline using "os.h" as os;

struct Authorize {
    bool used;
    identity::Identity identity;
    buffer::Buffer+300 mut path;
}


struct Mem {
    identity::Secret    secret;
    identity::Secret    principal;
    bool                has_principal;
    identity::Address   network;
    identity::Secret    network_secret;

    Authorize auth[32];
    buffer::Buffer+2000 mut file_name;
}

export fn from_home_carriertoml(vault::Vault mut new *self, err::Err mut *e)
    where err::checked(*e)
    if #(os::defined(os::OPENWRT_BUILD))
{
    from_carriertoml(self, e, "/etc/config/devguard/carrier.toml");
}
else
{
    new+200 s = buffer::make();
    fs::get_homedir(e, &s);
    if err::check(e) {return;}

    // maybe create
    s.append_cstr("/.devguard");
    mkdir(s.mem, unsafe<int>(os::S_IRWXU));

    s.append_cstr("/carrier.toml");
    from_carriertoml(self, e, s.cstr());
}

fn close(vault::Vault mut*self) {
    if self->user != 0 {
        free(self->user);
        self->user = 0;
    }
}

export fn from_carriertoml_and_secret(vault::Vault mut new *self, err::Err mut *e, char * file_name, identity::Secret * secret)
    where err::checked(*e)
    where nullterm(file_name)
{
    i_from_carriertoml(self, e, file_name);
    if err::check(e) { return; }

    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    memcpy(this->secret.k, secret->k, static(len(this->secret.k)));

    identity::nullcheck(e, this->secret.k);
    if err::check(e) { return; }
}

export fn from_carriertoml(vault::Vault mut new *self, err::Err mut *e, char * file_name)
    where err::checked(*e)
    where nullterm(file_name)
{
    i_from_carriertoml(self, e, file_name);
    if err::check(e) { return; }

    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    identity::nullcheck(e, this->secret.k);
    if err::check(e) { return; }
}

fn i_from_carriertoml(vault::Vault mut new *self, err::Err mut *e, char * file_name)
    where err::checked(*e)
    where nullterm(file_name)
{
    memset(self, 0, sizeof(vault::Vault));

    self->i_get_local_identity      = i_get_local_identity;
    self->i_sign_local              = i_sign_local;
    self->i_get_principal_identity  = i_get_principal_identity;
    self->i_sign_principal          = i_sign_principal;
    self->i_get_network             = i_get_network;
    self->i_get_network_secret      = i_get_network_secret;
    self->i_get_secretkit           = i_get_secretkit;
    self->i_advance_clock           = i_advance_clock;
    self->i_set_network             = i_set_network;
    self->i_list_authorizations     = i_list_authorizations;
    self->i_del_authorization       = i_del_authorization;
    self->i_add_authorization       = i_add_authorization;
    self->i_close                   = close;


    self->user = malloc(sizeof(Mem));
    Mem mut * this = (Mem mut *)self->user;
    memset(this, 0, sizeof(Mem));
    static_attest(safe(this));

    new+1000 parser = toml::parser(e, toml::U{
        it: load_from_toml_cb,
        user1: this
    });
    if err::check(e) {return;}

    this->file_name.make();
    this->file_name.append_cstr(file_name);

    let mut f = (FILE mut*)fopen(this->file_name.cstr(), "r");
    if f == 0 {
        f = (FILE mut*)fopen(this->file_name.cstr(), "w");
        if f == 0 {
            err::fail_with_errno(e, "open %s", this->file_name.cstr());
            return;
        }
        fclose(f);

        identity::secret_generate (&this->secret, e);
        if err::check(e) {return;}
        identity::secret_generate (&this->network_secret, e);
        if err::check(e) {return;}
        identity::address_from_secret(&this->network, &this->network_secret);

        save_to_toml(this, e);
        if err::check(e) {return;}

        log::info("created %s\n", this->file_name.cstr());
        f = (FILE mut*)fopen(this->file_name.cstr(), "r");
        if f == 0 {
            err::fail_with_errno(e, "open %s", this->file_name.cstr());
            return;
        }
    }

    char mut buf[300];
    for(;;) {
        usize bin = as<usize>(fread(buf, sizeof(char), static(len(buf)), f));
        if bin == 0 {
            break;
        }
        static_attest(bin < len(buf));
        parser.push(e, buf, bin);
        if err::check(e) { fclose(f); return; }
    }
    fclose(f);

    parser.close(e);
    if err::check(e) { return; }


    if identity::isnull(this->network.k) {
        identity::secret_generate (&this->network_secret, e);
        if err::check(e) {return;}
        identity::address_from_secret(&this->network, &this->network_secret);
        save_to_toml(this, e);
        if err::check(e) {return;}
    }

    if identity::isnull(this->secret.k) {
        identity::secret_generate (&this->secret, e);
        if err::check(e) {return;}
        save_to_toml(this, e);
        if err::check(e) {return;}
    }
}


fn load_from_toml_cb_publish(toml::U *u, err::Err mut *e, toml::Parser mut* p, char *k, toml::Value v)
    where err::checked(*e)
    where nullterm(k)
{
    let this = (Mem mut*)u->user1;
    static_attest(safe(this));

    if buffer::cstr_eq(k, "shadow") || buffer::cstr_eq(k, "address") {
        err::assert_safe(v.string);
        static_attest(nullterm(v.string));
        identity::address_from_cstr (&this->network, e, v.string);
        if err::check(e) { return; }
    } else if buffer::cstr_eq(k, "secret") {
        err::assert_safe(v.string);
        static_attest(nullterm(v.string));
        identity::secret_from_cstr (&this->network_secret, e, v.string);
        if err::check(e) { return; }
    }
}

fn load_from_toml_authorize_iter(toml::U *self, err::Err mut *e, toml::Parser mut* p, char *k, toml::Value v)
    where err::checked(*e)
    where nullterm(k)
{
    let this = (Mem mut*)self->user1;
    static_attest(safe(this));

    static_attest(self->user2 < len(this->auth));
    Authorize mut *a = this->auth + self->user2;

    if buffer::cstr_eq(k, "identity") {
        err::assert_safe(v.string);
        static_attest(nullterm(v.string));
        identity::identity_from_cstr (
            &a->identity,
            e,
            v.string
        );
        if err::check(e) { return; }
    } else if buffer::cstr_eq(k, "resource") {
        a->path.make();
        a->path.append_cstr(v.string);
    }

    if !identity::isnull(a->identity.k) && a->path.slen() > 0 {
        a->used = true;
    }
}

fn load_from_toml_identity_secret(Mem mut *that, err::Err mut *e,  toml::Value v)
    where err::checked(*e)

if #(os::defined(os::OPENWRT_BUILD))
{
    static_attest(nullterm(v.string));
    static_attest(safe(v.string));

    new+400 part = buffer::make();
    new+400 vv = buffer::make();
    vv.append_cstr(v.string);

    usize mut iterator = 0;
    if vv.split(':', &iterator, &part) && vv.split(':', &iterator, &part) {
        if part.eq_cstr("mtdblock") {
            part.clear();
            if vv.split(':', &iterator, &part) {

                usize mtdindex = findmtdindex(e, part.cstr());
                if err::check(e) { return; }

                int mut offset = 0;

                part.clear();
                if vv.split(':', &iterator, &part) {
                    offset = unsafe<int>(os::atoi(part.mem));
                }

                new+1000 mtdpath = buffer::make();
                mtdpath.format("/dev/mtdblock%u", mtdindex);

                log::info("reading secrets from %s:%d", mtdpath.mem, offset);

                FILE mut * f = fopen(mtdpath.mem, "a+");
                if f == 0 {
                    e->fail_with_errno("cannot open %s", mtdpath.cstr());
                    return;
                }
                if unsafe<int>(fseek(f, offset, SEEK_SET)) < 0 {
                    e->fail_with_errno("cannot seek %s", mtdpath.cstr());
                    fclose(f);
                    return;
                }

                if unsafe<int>(fread(that->secret.k, 1, 32, f)) != 32 {
                    e->fail_with_errno("cannot read %s", mtdpath.mem);
                    fclose(f);
                    return;
                }

                if identity::isnull(that->secret.k) {
                    identity::secret_generate(&that->secret, e);
                    if err::check(e) {
                        fclose(f);
                        return;
                    }
                    if unsafe<int>(fseek(f, offset, SEEK_SET)) < 0 {
                        e->fail_with_errno("cannot seek %s", mtdpath.mem);
                        fclose(f);
                        return;
                    }

                    if unsafe<int>(fwrite(that->secret.k, 1, 32, f)) != 32 {
                        e->fail_with_errno("cannot write %s", mtdpath.mem);
                        fclose(f);
                        return;
                    }
                    if unsafe<int>(fseek(f, offset, SEEK_SET)) < 0 {
                        e->fail_with_errno("cannot seek %s", mtdpath.mem);
                        fclose(f);
                        return;
                    }

                    if unsafe<int>(fread(that->secret.k, 1, 32, f)) != 32 {
                        e->fail_with_errno("cannot read %s", mtdpath.mem);
                        fclose(f);
                        return;
                    }
                }

                fclose(f);
                return;
            }
        }
    }


    identity::secret_from_cstr((identity::Secret mut*)&that->secret, e, v.string);
}
else
{
    static_attest(nullterm(v.string));
    static_attest(safe(v.string));
    identity::secret_from_cstr((identity::Secret mut*)&that->secret, e, v.string);
}

fn findmtdindex(err::Err mut *e, char *mtdname) -> usize
    where err::checked(*e)
    where nullterm(mtdname)
{
    FILE mut * f = fopen("/proc/mtd", "r");
    new+1000 line = buffer::make();
    for (; line.fgets(f); line.clear()) {

        usize mut iterator = 0;

        new+100 dev = buffer::make();
        if !line.split(' ', &iterator, &dev) { continue; }

        new+100 size = buffer::make();
        if !line.split(' ', &iterator, &size) { continue; }

        new+100 erasesize = buffer::make();
        if !line.split(' ', &iterator, &erasesize) { continue; }

        new+100 name = buffer::make();
        if !line.split(' ', &iterator, &name) { continue; }

        new+100 name_cleaned = buffer::make();
        usize mut iterator2 = 1;
        if !name.split('"', &iterator2, &name_cleaned) { continue; }

        if name_cleaned.eq_cstr(mtdname) {
            usize index = unsafe<usize>(atoi(dev.mem + 3));
            fclose(f);
            return index;
        }
    }

    fclose(f);
    e->fail(err::InvalidArgument, "cannot find mtdblock %s", mtdname);
    return 0;
}

fn load_from_toml_cb(toml::U *u, err::Err mut *e, toml::Parser mut* p, char *k, toml::Value v)
    where err::checked(*e)
    where nullterm(k)
{

    let this = (Mem mut*)u->user1;
    static_attest(safe(this));
    static_attest(nullterm(v.string));
    static_attest(nullterm(k));
    static_attest(safe(v.string));

    if buffer::cstr_eq(k, "publish") && v.t == toml::ValueType::Object {
        toml::next(p, e, toml::U{it: load_from_toml_cb_publish, user1: this});
    } else if buffer::cstr_eq(k, "network") && v.t == toml::ValueType::Object {
        toml::next(p, e, toml::U{it: load_from_toml_cb_publish, user1: this});
    } else if buffer::cstr_eq(k, "authorize") && v.t == toml::ValueType::Object {
        for (usize mut i = 0; i < static(len(this->auth)); i++) {
            if !this->auth[i].used {
                toml::next(p, e, toml::U{
                    it:     load_from_toml_authorize_iter,
                    user1:  this,
                    user2:  i,
                });
                break;
            }
        }
    } else if v.t == toml::ValueType::String && buffer::cstr_eq(k, "secret") {
        load_from_toml_identity_secret(this, e, v);
    } else if v.t == toml::ValueType::String && buffer::cstr_eq(k, "principal") {
        identity::secret_from_cstr((identity::Secret mut*)&this->principal, e, v.string);
        this->has_principal = true;
    }
}




fn save_to_toml(Mem *self, err::Err mut *e)
    where err::checked(*e)
{
    static_attest(buffer::integrity(&self->file_name));
    let out = (FILE mut*)fopen(self->file_name.cstr(), "w");
    if out == 0 {
        err::fail_with_errno(e, "open %s", self->file_name.cstr());
        return;
    }

    char mut buf[120] = {0};
    identity::secret_to_str(e, buf, static(len(buf)), (identity::Secret*)&self->secret);
    if err::check(e) { fclose(out); return; }
    fprintf(out , "secret = \"%s\"\n", buf);

    if self->has_principal && !identity::isnull(self->principal.k)  {
        memset(buf, 0, static(len(buf)));
        identity::secret_to_str(e, buf, static(len(buf)), &self->principal);
        if err::check(e) { fclose(out); return; }
        fprintf(out, "principal = \"%s\"\n", buf);
    }

    if !identity::isnull(self->network.k) {
        memset(buf, 0, static(len(buf)));
        identity::address_to_str(e, buf, static(len(buf)), &self->network);
        if err::check(e) { fclose(out); return; }
        fprintf(out , "\n[network]\naddress = \"%s\"\n", buf);

        if !identity::isnull(self->network_secret.k) {
            memset(buf, 0, static(len(buf)));
            identity::secret_to_str(e, buf, static(len(buf)), &self->network_secret);
            if err::check(e) { fclose(out); return; }
            fprintf(out, "secret = \"%s\"\n", buf);
        }
    }

    for (usize mut i = 0; i < static(len(self->auth)); i++) {
        if !self->auth[i].used {
            continue;
        }
        memset(buf, 0, static(len(buf)));
        identity::identity_to_str(e, buf, static(len(buf)), &self->auth[i].identity);
        fprintf(out , "\n[[authorize]]\nidentity = \"%s\"\nresource = ", buf);
        fprintf(out , "\"%.*s\"\n", (int)self->auth[i].path.at, self->auth[i].path.mem);
    }

    fprintf(out , "\n");
    fclose(out);
}


fn i_get_local_identity (vault::Vault *self, identity::Identity mut *id)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    identity::identity_from_secret(id, &this->secret);
}

fn i_sign_local(vault::Vault * self, identity::Signature mut *s, u8 * subject, usize subject_len)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    identity::sign(&this->secret, s, subject, subject_len);
}

fn i_get_principal_identity (vault::Vault *self, identity::Identity mut *id)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    if !this->has_principal {
        i_get_local_identity(self, id);
        return;
    }
    identity::identity_from_secret(id, &this->principal);
}

fn i_sign_principal(vault::Vault * self, identity::Signature mut *s, u8 * subject, usize subject_len)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    if !this->has_principal {
        i_sign_local(self, s, subject, subject_len);
        return;
    }
    identity::sign(&this->principal, s, subject, subject_len);
}

fn i_get_network (vault::Vault *self, identity::Address mut * network)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    memcpy(network, &this->network, sizeof(identity::Address));
}

fn i_get_network_secret (vault::Vault *self, identity::Secret mut * secret)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    memcpy(secret, &this->network_secret, sizeof(identity::Secret));
}

fn i_get_secretkit (vault::Vault *self, identity::SecretKit mut * secret)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    memcpy(&secret->network, &this->network_secret,     sizeof(identity::Secret));
    if this->has_principal {
        memcpy(&secret->identity,  &this->principal,    sizeof(identity::Secret));
    } else {
        memcpy(&secret->identity,  &this->secret,       sizeof(identity::Secret));
    }
}

fn i_advance_clock (vault::Vault *self) -> u64
{
    let now = time::real();
    return now.to_millis();
}

fn i_set_network (vault::Vault *self, err::Err mut *e, identity::Secret * secret)
    where err::checked(*e)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    memcpy(&this->network_secret, secret, sizeof(identity::Secret));
    identity::address_from_secret(&this->network, &this->network_secret);

    save_to_toml(this, e);
}

fn i_del_authorization(vault::Vault mut * self, err::Err mut *e, identity::Identity *delme, char *resource)
    where err::checked(*e)
    where nullterm(resource)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    for (usize mut i = 0; i < static(len(this->auth)); i++) {
        if !this->auth[i].used {
            continue;
        }
        let fe = &this->auth[i].path;
        static_attest(buffer::integrity(fe));
        if this->auth[i].identity.eq(delme) && fe->eq_cstr(resource) {
            this->auth[i].used = false;
            memset(this->auth + i, 0, sizeof(Authorize));
        }
    }
    save_to_toml(this, e);
}

fn i_add_authorization(vault::Vault mut * self,err::Err mut *e, identity::Identity *addme, char *resource)
    where err::checked(*e)
    where nullterm(resource)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    for (usize mut i = 0; i < static(len(this->auth)); i++) {
        if this->auth[i].used {
            continue;
        }
        memcpy(&this->auth[i].identity, addme, sizeof(identity::Identity));
        this->auth[i].path.make();

        let fe = &this->auth[i].path;
        static_attest(buffer::integrity(fe));
        fe->append_cstr(resource);

        this->auth[i].used = true;
        save_to_toml(this, e);
        return;
    }
    err::fail(e, err::OutOfTail, "too many [[authorize]] sections");
    return;
}

fn i_list_authorizations(vault::Vault * self, err::Err mut *e, vault::list_authorizations_cb cb, void mut * unsafe user)
    where err::checked(*e)
    where safe(cb)
{
    let this = (Mem mut*)self->user;
    static_attest(safe(this));

    for (usize mut i = 0; i < static(len(this->auth)); i++) {
        if !this->auth[i].used {
            continue;
        }
        let fe = &this->auth[i].path;
        static_attest(buffer::integrity(fe));
        cb(user, &this->auth[i].identity, fe->cstr());
    }
}
