/// noise specific implementation of chachapoly
///
/// ciphertext has 16 bytes hmac at the end

using "../3rdparty/hacl-star/snapshots/hacl-c/Hacl_Chacha20Poly1305.h" as hacl_chacha;
using <assert.h>::{assert};
using <string.h>::{memcpy, memset};
using sha256;
using err;
using byteorder;

export struct CipherState {
    u8      mut key[32];
    bool    mut has_key;
}

export fn init(CipherState mut * self, u8 *k) {
    memcpy(self->key, k, 32);
    self->has_key = true;
}


export symbol EncryptionError;

export fn encrypt_ad(
    CipherState mut*    self,
    err::Err+et mut* e,
    u8*         unsafe authtext,
    usize       authtext_len,
    u8 *        plain,
    usize       plainlen,
    u64         nonce,
    u8 mut*     ciphertext,
    usize       cipherlen_max,
) -> usize
    where err::checked(*e)
    where len(ciphertext) >= cipherlen_max
    where len(plain)      >= plainlen
    where len(ciphertext) > plainlen
    model return <= cipherlen_max
{
    assert(self->has_key);

    usize cipherlen = plainlen + 16;
    assert(cipherlen_max >= cipherlen);
    static_attest(cipherlen_max >= cipherlen);

    u8 mut* mac = ciphertext + plainlen;

    //noise spec: The 96-bit nonce is formed by encoding 32 bits of zeros followed by little-endian encoding of n.
    u8  mut nonce12[12] = {0};
    u64 nonce_le = byteorder::to_le64(nonce);
    memcpy(nonce12 + 4, (u8*)&nonce_le, 8);

    int xr = as<int>(hacl_chacha::Hacl_Chacha20Poly1305_aead_encrypt(
        ciphertext,
        mac,
        (u8 mut*)plain,
        plainlen,
        (u8 mut*)authtext,
        authtext_len,
        self->key,
        nonce12
    ));
    if xr != 0 {
        err::fail(e, EncryptionError, "encrypt");
        return 0;
    }
    return cipherlen;
}

export fn decrypt_ad(
    CipherState mut*    self,
    err::Err+et mut* e,
    u8*     unsafe authtext,
    usize   authtext_len,
    u8 *    ciphertext,
    usize   cipherlen,
    u64     nonce,
    u8 mut* plain,
    usize   plainlen_max,
) -> usize
    where err::checked(*e)
    where len(ciphertext) >= cipherlen
    where len(plain)      >= plainlen_max
{
    assert(self->has_key);
    usize plainlen = cipherlen - 16;

    assert(plainlen_max >= plainlen);
    static_attest(plainlen_max >= plainlen);

    assert(cipherlen > 16);
    static_attest(cipherlen > 16);

    u8 * mac = ciphertext + plainlen;

    u8  mut nonce12[12] = {0};
    u64 nonce_le = byteorder::to_le64(nonce);
    memcpy(nonce12 + 4, (u8*)&nonce_le, 8);

    int xr = as<int>(hacl_chacha::Hacl_Chacha20Poly1305_aead_decrypt(
        plain,
        (u8 mut*)ciphertext,
        plainlen,
        (u8 mut*)mac,
        (u8 mut*)authtext,
        authtext_len,
        self->key,
        nonce12
    ));
    if xr != 0 {
        err::fail(e, EncryptionError, "decrypt");
        return 0;
    }
    return plainlen;
}


export fn encrypt(
    CipherState mut*    self,
    err::Err+et set<err> mut* e,
    u8 *        plain,
    usize       plainlen,
    u64         nonce,
    u8 mut*     ciphertext,
    usize       cipherlen_max,
) -> usize
    where err::checked(*e)
    where len(ciphertext) >= cipherlen_max
    where len(plain)      >= plainlen
    where len(ciphertext) > plainlen
    model return <= cipherlen_max
{
    return encrypt_ad(self, e, 0,0, plain, plainlen, nonce, ciphertext, cipherlen_max);
}


export fn decrypt(
    CipherState mut*    self,
    err::Err+et set<err> mut* e,
    u8 *    ciphertext,
    usize   cipherlen,
    u64     nonce,
    u8 mut* plain,
    usize   plainlen_max,
) -> usize
    where err::checked(*e)
    where len(ciphertext) >= cipherlen
    where len(plain)      >= plainlen_max
{
    return decrypt_ad(self, e, 0, 0, ciphertext, cipherlen, nonce, plain, plainlen_max);
}
