using identity;
using err;
using noise;
using std;
using hex;
using byteorder;
using <assert.h>::{assert};
using <stdio.h>::{printf};
using <memory.h>::{strlen, memcpy, memset};
using string;
using cipher;
using router;
using symmetric;
using bootstrap;
using carrier;
using crc8;
using channel;

static char PROLOGUE[] = "carrier has arrived";
static char SIGN_PURPOSE[] = "carrier handshake hash 1";

export struct Initiator {
    string::String+1024 mut pkt;
    noise::Initiator mut    noise;
}

//TODO this is minimal prototype code to get v9 working.
//     dont forget fuzzing when implementing v10
pub fn initiate(
    Initiator mut* self,
    err::Err+et mut* e,
    identity::Secret *secret,
    identity::Address * xaddr,
    u64 network_time,
)
    where err::checked(*e)
{
    memset(self, 0, sizeof(Initiator));

    identity::Identity mut eid = {0};
    identity::identity_from_secret(&eid, secret);

    std::memcpy(((self->noise).remote_static).k, xaddr->k, 32);

    identity::secret_generate(e, &(self->noise).ephermal);
    if err::check(e) {
        return ;
    }


    // encrypted header
    u8 mut plaintext[512] = {0};
    usize mut plaintext_len = 0;
    plaintext_len  += 2;

    memcpy(plaintext + plaintext_len, eid.k, 32);
    plaintext_len += 32;

    u64 network_time_be = byteorder::to_be64(network_time);
    memcpy(plaintext + plaintext_len, &network_time_be, 8);
    plaintext_len += 8;

    u32 revision_be = byteorder::to_be32(carrier::REVISION);
    memcpy(plaintext + plaintext_len, &revision_be, 4);
    plaintext_len += 4;

    // move target 32B
    //std::memcpy(body + at, eid.k, 32);
    //at += 32;

    // padding expected ciphertext size to nearest 64 byte before authentication tag
    plaintext_len += 64 - ((plaintext_len - 16)  % 64);


    printf("plaintext len %zu\n", plaintext_len);

    self->pkt.len  = 0;
    // version 1B
    (self->pkt.mem)[0] = 9;
    // reserved 3B
    // route    8B
    // counter  8B
    self->pkt.len += 4 + 8 + 8;

    // does this even do anything? the initiation header should actually be always the same
    plaintext[0] = crc8::broken_crc8(0, (u8*)self->pkt.mem, self->pkt.len);

    u8 mut* pp = (u8 mut*)self->pkt.mem + self->pkt.len;
    usize pplen = static(len(self->pkt.mem)) - self->pkt.len;
    static_attest(safe(pp));
    static_attest(len(pp) >= pplen);

    usize prologue_len = (usize)strlen(PROLOGUE);
    static_attest(prologue_len < len((u8*)PROLOGUE));


    printf("plaintext:\n");
    hex::dump(plaintext, plaintext_len);

    self->pkt.len += noise::initiate(
        &self->noise, e,
        pp, pplen,
        (u8*)PROLOGUE, prologue_len,
        plaintext, plaintext_len
    );

    if err::check(e) {
        return;
    }

    printf("ciphertext len %zu\n", self->pkt.len - 4 - 8 - 8);
    assert((self->pkt.len - 4 - 8 - 8) % 64 == 0);


    printf("handshake hash:\n");
    hex::dump(((self->noise).symm).h, static(len(((self->noise).symm).h)));

    printf("identity:\n");
    hex::dump(eid.k, 32);

    //sign handshake hash
    u8 mut signme[static(len(SIGN_PURPOSE)) + static(len(((self->noise).symm).h))] = {0};
    std::memcpy(signme, SIGN_PURPOSE, string::strlen(SIGN_PURPOSE));
    u8 mut * mem = signme + string::strlen(SIGN_PURPOSE);

    std::memcpy(mem, ((self->noise).symm).h, static(len(((self->noise).symm).h)));


    identity::Signature mut*sig= (identity::Signature mut*)(self->pkt.mem + self->pkt.len);
    static_attest(safe(sig));
    identity::sign(secret, sig, signme, static(len(signme)));
    self->pkt.len += 64;
    printf("packet len %zu\n", self->pkt.len);
    static_attest(len(self->pkt.mem) >= self->pkt.len);
    hex::dump((u8*)self->pkt.mem, self->pkt.len);
}


pub fn complete (
    Initiator       mut* self,
    err::Err+et     mut* e,

    channel::Channel    mut* broker_chan,
    bootstrap::Broker   mut* redirect,
)
    where err::checked(*e)
{
    printf("completing with %zu\n", self->pkt.len);
    static_attest(len(self->pkt.mem) >  self->pkt.len);
    hex::dump((u8*)self->pkt.mem, self->pkt.len);

    if self->pkt.len < 4 + 8 + 8 + 32 + 16  + 64 {
        err::fail(e, 1, "too small");
        return;
    }

    // version 1B
    if (self->pkt.mem)[0] != 9 {
        err::fail(e, 1, "invalid version");
        return;
    }
    // reserved 3B
    // route    8B
    u8 *rkeymem = (u8*)self->pkt.mem + 4;
    static_attest(safe(rkeymem));
    static_attest(len(rkeymem) > 8);
    u64 route = router::read_routing_key(rkeymem, 0);
    // counter  8B
    u64 counter = byteorder::from_be64(*&(self->pkt.mem)[12]);
    printf("<> counter %lu\n", counter);
    usize mut at = 4 + 8 + 8;


    u8 mut *ciphertext   = (u8 mut*)self->pkt.mem + at;
    usize ciphertext_len = self->pkt.len - at - 64;
    static_attest(safe(ciphertext));
    static_attest(len(ciphertext) >= ciphertext_len);

    u8 mut cleartext[1024] = {0};

    usize cleartext_len = noise::complete(
        &self->noise,
        e,
        cleartext,
        static(len(cleartext)),
        ciphertext,
        ciphertext_len
    );

    if err::check(e) {
        return;
    }

    printf("received cleartext:\n");
    hex::dump(cleartext, cleartext_len);

    if cleartext_len < 32 + 2 + 8 + 4 {
        err::fail(e, 1, "too small");
        return;
    }

    // flags
    bool flag_mov       = (cleartext[1] & 0b01000000) != 0;
    u8 * mut pp = cleartext + 2;

    // remote identity
    identity::Identity mut r_identity = {0};
    static_attest(safe(pp));
    static_attest(len(pp) >= 32);
    std::memcpy(r_identity.k, pp, 32);

    pp = pp + 32;

    // timestamp
    u64 mut timestamp = 0;
    unsafe {
        timestamp = *(u64*)pp;
    }
    pp = pp + 8;


    //check handshake signature
    u8 mut signme[static(len(SIGN_PURPOSE)) + static(len(((self->noise).symm).h))] = {0};
    std::memcpy(signme, SIGN_PURPOSE, string::strlen(SIGN_PURPOSE));
    u8 mut* mem = signme + string::strlen(SIGN_PURPOSE);
    std::memcpy(mem, ((self->noise).symm).h, static(len(((self->noise).symm).h)));
    identity::Signature mut * mut sig;
    unsafe {
        sig = (identity::Signature mut *)(self->pkt.mem + self->pkt.len - 64);
    }
    static_attest(safe(sig));
    if !identity::verify(&r_identity, sig, signme, static(len(signme))) {
        err::fail(e, 1, "invalid signature");
        return;
    }

    char mut r_identity_str[128] = {0};
    identity::identity_to_str(e, r_identity_str, static(len(r_identity_str)), &r_identity);
    if err::check(e) {
        return;
    }

    // parse the rest of the cleartext, after signature passed


    // move instructions
    if flag_mov {
        u16 mut instruction_len = 0;
        unsafe {
            instruction_len = byteorder::from_be64(*(u64*)pp);
        }
        pp = pp + 8;

        string::String+300 mut movinstr = {0};
        string::append_bytes(&movinstr, pp, (usize)instruction_len);

        if bootstrap::parse_record(&movinstr, redirect) {
            printf("cluster move %.*s\n", (int)movinstr.len, movinstr.mem);
            return;
        }
    }


    printf("connected to broker %s\n", r_identity_str);
    *broker_chan = channel::from_symmetric(
        &self->noise.symm,
        route,
        false);
}
