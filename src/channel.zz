using cipher;
using symmetric;
using <stdio.h>::{printf, fprintf, stderr};
using err;
using hex;
using byteorder;
using pq;
using std;
using crc8;
using <assert.h>::{assert};

pub struct Channel {
    u64     route;
    bool    responder;
    u64     mut counter_out;
    cipher::CipherState mut c_mine;
    cipher::CipherState mut c_them;
    pq::Q   mut q;

    u64     mut outgoing_acks[32];
    u8      mut outgoing_acks_at;
}


pub fn from_symmetric(symmetric::SymmetricState * symm, u64 route, bool responder) -> Channel {
    Channel mut c = Channel{
        route      : route,
        responder  : responder,
    };

    if responder {
        symmetric::split(symm, &c.c_them, &c.c_mine);
    } else {
        symmetric::split(symm, &c.c_mine, &c.c_them);
    }

    c.counter_out = 1;
    c.outgoing_acks_at = 1;

    return c;
}

pub fn pull(Channel mut* self, err::Err+et mut* e, u8 mut* mut pkt, usize max_pkt) -> usize
    where err::checked(*e)
    where max_pkt  >= len(pkt)
    where len(pkt) >= 4 + 8 + 8 + 320
{
    if self->outgoing_acks_at == 0 {
        return 0;
    }

    assert(max_pkt >= 4 + 8 + 8 + 320);

    u64 counter_be = byteorder::to_be64(self->counter_out+ 1 /* rust version has an off by one*/);
    u8* counter_b = (u8*)&counter_be;

    u64 mut route   = self->route;
    u8 mut* mut route_b = (u8 mut*)&route;
    if self->responder {
        unsafe {
        route_b[7] |= 0b00000001;
        }
    }

    // version 1B
    pkt[0] = 0x09;
    // reserved 3B
    pkt[1] = 0x00;
    pkt[2] = 0x00;
    pkt[3] = 0x00;
    unsafe{
    std::memcpy(pkt + 4,     route_b,    8);
    std::memcpy(pkt + 4 + 8, counter_b,  8);
    }

    usize mut pkt_size = 4 + 8 + 8;
    usize unencrypted_size = pkt_size;

    //broken header crc
    pkt[pkt_size] = crc8::broken_crc8(0, pkt, 4 + 8 + 8);
    pkt_size += 1;

    //flags
    pkt[pkt_size] = 0x00;
    pkt_size += 1;

    //payload size
    u16 mut * payload_size = (u16 mut*)(pkt + pkt_size);
    pkt_size += 2;


    usize size_before_payload = pkt_size;


    // all outgoing acks will fit in because 32 * 8 = 256
    // if they did not, we would need a fifo
    if self->outgoing_acks_at > 0 {

        // frame type
        pkt[pkt_size] = 0x01;
        pkt_size += 1;

        // ack delay
        pkt[pkt_size]       = 0x00;
        pkt[pkt_size + 1]   = 0x01;
        pkt_size += 2;

        // number of acks
        pkt[pkt_size]       = 0x00;
        pkt[pkt_size + 1]   = self->outgoing_acks_at;
        pkt_size += 2;

        static_attest(len(self->outgoing_acks) > self->outgoing_acks_at);
        for (usize mut i = 0; i < (usize)self->outgoing_acks_at ; i++) {
            assert(pkt_size < max_pkt);
            u64 ackd  = byteorder::to_be64((self->outgoing_acks)[i]);
            unsafe {
            std::memcpy(pkt + pkt_size, &ackd, 8);
            }
            pkt_size += 8;
        }
        self->outgoing_acks_at = 0;
    }

    unsafe {
        (*payload_size) = byteorder::to_be16(pkt_size - size_before_payload);
    }

    // padding to %64
    pkt_size += 64 - ((pkt_size + 16) % 64);
    if pkt_size > max_pkt {
        pkt_size = max_pkt;
    }

    usize padded_size = pkt_size - unencrypted_size;
    usize budget      = max_pkt - unencrypted_size;

    static_attest(pkt_size < len(pkt));
    hex::dump(pkt, pkt_size);

    u8* plaintext = pkt + 4 + 8 + 8;
    static_attest(safe(plaintext));

    u8 mut* ciphertext = pkt + unencrypted_size;
    static_attest(safe(ciphertext));

    static_attest(len(ciphertext) >= padded_size);
    static_attest(len(ciphertext) >= budget);

    usize cipherlen = cipher::encrypt(
        &(self->c_mine),
        e,
        ciphertext,
        padded_size,
        self->counter_out,
        ciphertext,
        budget
    );

    if err::check(e) {
        return 0;
    }

    self->counter_out += 1;

    return unencrypted_size + cipherlen;
}

pub fn push(Channel mut* self, err::Err+et mut* e, u8 mut* body, usize l, u64 counter)
    where err::checked(*e)
    where len(body) >= l
    where l > 16
{
    // counter is offset by one in the rust implementation
    u64 realcounter = counter - 1;

    usize plainlen = cipher::decrypt(
        &(self->c_them),
        e,
        body,
        l - 16,
        realcounter,
        body,
        l
    );
    if err::check(e) {
        return;
    }

    // broken crc and flags 2B
    // payload len 2B
    usize mut frames_len;
    unsafe {frames_len = byteorder::from_be16(*(u16*)(body + 2)) }
    u8 *frames = body + 4;
    if frames_len >= plainlen - 4 {
        err::fail(e, 192, "corrupt");
        return;
    }

    for (usize mut i = 0; i < frames_len; i++) {
        static_attest(i < len(frames));
        static_attest(i < len(body));
        switch frames[i] {
            0 => {}
            2 => {
                printf("PING!\n");
                ack(self, e, counter);
                if err::check(e) {
                    return;
                }
            }
            default => {
                err::fail(e, 194, "invalid frame %u", body[i]);
                return;
            }
        }
    }
}

pub fn ack(Channel mut* self, err::Err mut* e, u64 ackd) {
    if (usize)self->outgoing_acks_at < (usize)sizeof(self->outgoing_acks) {
        static_attest((usize)self->outgoing_acks_at < len(self->outgoing_acks));
        (self->outgoing_acks)[(usize)self->outgoing_acks_at] = ackd;
        self->outgoing_acks_at+=1;
    } else {
        fprintf(stderr, "outgoing ack buffer overflow\n");
    }
}


