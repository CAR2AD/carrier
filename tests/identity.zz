using carrier;
using carrier::identity::{
    Alias,
    Address,
    Identity,
    Secret,
    identity_from_str,
    identity_from_cstr,
    identity_from_secret,
    identity_to_str,
    alias_to_str,
    secret_from_str,
    secret_from_cstr,
    secret_to_str,
    address_from_secret,
    address_to_str,
    address_from_str,
    address_from_cstr,
    alias_from_str,
    secret_generate,
};
using err;
using <string.h>::{memcmp};
using <stdio.h>::{printf};
using <assert.h>::{assert};

fn test_identity(err::Err+et mut* e)
    where err::checked(*e)
{
    new sk = secret_generate(e);
    if err::check(e) { return; }

    Identity mut pk = {0};
    identity_from_secret(&pk, &sk);

    char mut buf[200] = {0};
    identity_to_str(e, buf, static(len(buf)), &pk);
    if err::check(e) { return; }

    printf("%s\n", buf);

    new pk2 = identity_from_cstr(e, buf);
    if err::check(e) { return; }

    assert(memcmp(pk.k, pk2.k, sizeof(pk.k)) == 0);

    secret_to_str(e, buf, static(len(buf)), &sk);
    if err::check(e) { return; }

    printf("%s\n", buf);

    new sk2 = secret_from_cstr(e, buf);
    if err::check(e) { return; }

    assert(memcmp(sk.k, sk2.k, sizeof(sk.k)) == 0);
}

fn test_corrupt_identity(err::Err+et mut* e)
    where err::checked(*e)
{
    char *from = "oWVXR9hGa6QgKLGrPaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa";

    new sk = secret_from_cstr(e, from);
    if !err::check(e) {
        err::fail(e, err::InvalidArgument, "must fail");
        return;
    }
    e->error = 0;
    static_attest(err::checked(*e));

    new ik = identity_from_cstr(e, from);
    if !err::check(e) {
        err::fail(e, err::InvalidArgument, "must fail");
        return;
    }
    e->error = 0;
}

fn test_address(err::Err+et mut* e)
    where err::checked(*e)
{
    new sk = secret_generate(e);
    if err::check(e) { return; }

    Address mut pk = {0};
    address_from_secret(&pk, &sk);

    char mut buf[100] = {0};
    address_to_str(e, buf, static(len(buf)), &pk);
    if err::check(e) { return; }

    printf("%s\n", buf);

    new pk2 = address_from_cstr(e,buf);
    if err::check(e) { return; }

    assert(memcmp(pk.k, pk2.k, sizeof(pk.k)) == 0);
}

fn test_short(err::Err+et mut* e)
    where err::checked(*e)
{
    new sk = secret_generate(e);
    if err::check(e) { return; }

    Identity mut pk = {0};
    identity_from_secret(&pk, &sk);

    pk.k[0] = 0x00;
    pk.k[1] = 0x00;
    pk.k[2] = 0x00;
    pk.k[3] = 0x00;
    pk.k[4] = 0x00;
    pk.k[6] = 0x00;

    char mut buf[200] = {0};
    identity_to_str(e, buf, static(len(buf)), &pk);
    if err::check(e) { return; }

    printf("%s\n", buf);

    new pk2 = identity_from_cstr(e, buf);
    if err::check(e) { return; }

    assert(memcmp(pk.k, pk2.k, sizeof(pk.k)) == 0);

    secret_to_str(e, buf, static(len(buf)), &sk);
    if err::check(e) { return; }

    printf("%s\n", buf);

    new sk2 = secret_from_cstr(e, buf);
    if err::check(e) { return; }

    assert(memcmp(sk.k, sk2.k, sizeof(sk.k)) == 0);
}

fn test_alias(err::Err+et mut* e)
    where err::checked(*e)
{
    new sk = secret_generate(e);
    if err::check(e) { return; }

    Alias mut pk = {0};
    pk.k[4] = sk.k[0];
    pk.k[5] = sk.k[1];
    pk.k[6] = sk.k[2];
    pk.k[7] = sk.k[3];

    char mut buf[100] = {0};
    usize ll = alias_to_str(e, buf, static(len(buf)), &pk);
    static_attest(ll < len(buf));
    if err::check(e) { return; }

    for (usize mut i = 0; i < ll; i++) {
        printf("%c", buf[i]);
        if i % 4 == 0 {
            printf(" ");
        }
    }
    printf("\n", buf);

    new pk2 = alias_from_str(e, buf, ll);
    if err::check(e) { return; }
    assert(memcmp(pk.k, pk2.k, sizeof(pk.k)) == 0);
}

export fn main() -> int {
    new+1000 e = err::make();

    test_identity(&e);
    err::abort(&e);

    test_corrupt_identity(&e);
    err::abort(&e);

    test_address(&e);
    err::abort(&e);

    test_short(&e);
    err::abort(&e);

    test_alias(&e);
    err::abort(&e);

    return 0;
}
